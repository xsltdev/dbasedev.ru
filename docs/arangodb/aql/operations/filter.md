# FILTER

Оператор `FILTER` можно использовать для ограничения результатов элементами, которые соответствуют произвольному логическому условию.

## Синтаксис

<pre><code>FILTER <em>expression</em></code></pre>

_expression_ должно быть условием, которое оценивается как `false` или `true`.

## Использование

Если результат условия равен false, текущий элемент пропускается, поэтому он не будет обрабатываться дальше и не станет частью результата. Если условие истинно, то текущий элемент не пропускается и может быть обработан дальше.

Список операторов сравнения, логических операторов и т.д., которые можно использовать в условиях, см. в разделе [Операторы](../operators.md).

<!-- 0001.part.md -->

```aql
FOR u IN users
  FILTER u.active == true && u.age < 39
  RETURN u
```

<!-- 0002.part.md -->

В запросе разрешается указывать несколько операторов `FILTER`, даже в одном и том же блоке. Если используется несколько операторов `FILTER`, их результаты будут объединены логическим `AND`, то есть все условия фильтра должны быть истинными, чтобы включить элемент.

<!-- 0003.part.md -->

```aql
FOR u IN users
  FILTER u.active == true
  FILTER u.age < 39
  RETURN u
```

<!-- 0004.part.md -->

В приведенном выше примере все элементы массива `users`, имеющие атрибут `active` со значением `true` и атрибут `age` со значением меньше `39` (включая `null`), будут включены в результат. Все остальные элементы `users` будут пропущены и не будут включены в результат, выдаваемый `RETURN`.

!!!info ""

    Описание влияния несуществующих или нулевых атрибутов см. в [Accessing Data from Collections](fundamentals-document-data.html).

Хотя `FILTER` обычно используется в комбинации с `FOR`, он также может использоваться на верхнем уровне или в подзапросах без окружающего цикла `FOR`.

<!-- 0005.part.md -->

```aql
FILTER false
RETURN ASSERT(false, "never reached")
```

<!-- 0006.part.md -->

## Порядок операций

Обратите внимание, что позиции операторов `FILTER` могут влиять на результат запроса. Например, в [тестовых данных](examples.html#example-data) 16 активных пользователей:

<!-- 0007.part.md -->

```aql
FOR u IN users
  FILTER u.active == true
  RETURN u
```

<!-- 0008.part.md -->

Мы можем ограничить набор результатов максимум 5 пользователями:

<!-- 0009.part.md -->

```aql
FOR u IN users
  FILTER u.active == true
  LIMIT 5
  RETURN u
```

<!-- 0010.part.md -->

Это может вернуть документы пользователей, например, Джима, Диего, Энтони, Майкла и Хлои. Какие из них будут возвращены, не определено, так как нет оператора `SORT` для обеспечения определенного порядка. Если мы добавим второй оператор `FILTER`, чтобы вернуть только женщин...

<!-- 0011.part.md -->

```aql
FOR u IN users
  FILTER u.active == true
  LIMIT 5
  FILTER u.gender == "f"
  RETURN u
```

<!-- 0012.part.md -->

... он может просто вернуть документ Chloe, потому что `LIMIT` применяется перед вторым `FILTER`. Во второй блок `FILTER` попадает не более 5 документов, и не все из них удовлетворяют критерию пола, даже если в коллекции более 5 активных пользователей женского пола. Более детерминированного результата можно добиться, добавив блок `SORT`:

<!-- 0013.part.md -->

```aql
FOR u IN users
  FILTER u.active == true
  SORT u.age ASC
  LIMIT 5
  FILTER u.gender == "f"
  RETURN u
```

<!-- 0014.part.md -->

Это вернет пользователей _Mariah_, _Mary_ и _Isabella_. Если отсортировать по возрасту в порядке `DESC`, то будут возвращены документы _Sophia_ и _Emma_. Однако `FILTER` после `LIMIT` не очень распространен, и вам, вероятно, нужен такой запрос:

<!-- 0015.part.md -->

```aql
FOR u IN users
  FILTER u.active == true AND u.gender == "f"
  SORT u.age ASC
  LIMIT 5
  RETURN u
```

<!-- 0016.part.md -->

Значение места размещения блоков `FILTER` позволяет этому единственному ключевому слову брать на себя роль двух ключевых слов SQL, `WHERE` и `HAVING`. Таким образом, `FILTER` в AQL работает с агрегатами `COLLECT` так же, как и с любым другим промежуточным результатом, атрибутом документа и т.д.

<!-- 0017.part.md -->
